module CapoHelpers

import {
    mkTv,
    tvCharter
} from StellarHeliosHelpers

import { Datum, Activity } from specializedCapo

import {
    requiresValidDelegateOutput
} from CapoDelegateHelpers


func getRefCharterUtxo(ctx: ScriptContext, mph : MintingPolicyHash) -> TxInput {
    chVal : Value = tvCharter(mph);
    hasCharter = (txin : TxInput) -> Bool { txin.value.contains(chVal) };
    print("getting ref_input for charter");
    charterUtxo : TxInput = ctx.tx.ref_inputs.find_safe(hasCharter).switch{
        Some{ch} => ch,
        None => error("Missing charter in required ref_inputs")
    };

    charterUtxo
}

func getRefCharterDatum(ctx: ScriptContext, mph : MintingPolicyHash) -> Datum::CharterToken {
    charterUtxo : TxInput = getRefCharterUtxo(ctx, mph);
    ctd : Datum::CharterToken = Datum::from_data( 
        charterUtxo.datum.get_inline_data() 
    );

    ctd
}

//! retrieves a required Charter Datum for the indicated policy - 
// ... either from the txn's reference inputs  or inputs.
func getTxCharterDatum(ctx: ScriptContext, mph : MintingPolicyHash) -> Datum::CharterToken {
    chVal : Value = tvCharter(mph);
    hasCharter = (txin : TxInput) -> Bool { txin.value.contains(chVal) };

    charterUtxo : TxInput = ctx.tx.ref_inputs.find_safe(hasCharter).switch{
        Some{ch} => ch,
        None => ctx.tx.inputs.find_safe(hasCharter).switch{
            Some{ch} => ch,
            None => error("Missing charter inputs / ref_inputs")
        }
    };
    ctd : Datum::CharterToken = Datum::CharterToken::from_data( 
        charterUtxo.datum.get_inline_data() 
    );

    ctd    
}

func mustHaveGovAuthority(ctx: ScriptContext, mph : MintingPolicyHash) -> Bool {
    charterDatum : Datum::CharterToken = getTxCharterDatum(ctx, mph);
    requiresValidDelegateOutput(charterDatum.govAuthorityLink, mph, ctx)
}

//! retrieves the redeemer for a specific input
func mustFindInputRedeemer(
    ctx : ScriptContext,
    txInput: TxInput
) -> Data {
    targetId : TxOutputId = txInput.output_id;
    redeemers : Map[ScriptPurpose]Data = ctx.tx.redeemers;
    spendsExpectedInput : ScriptPurpose = redeemers.find_key( 
        (purpose : ScriptPurpose) -> { purpose.switch{ 
            sp: Spending => {
                // print ("oid: " + sp.output_id.show());
                sp.output_id == targetId
            }, 
            _ => false 
        } }
    );
    redeemers.get(spendsExpectedInput)
}


